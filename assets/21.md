# `IO`以外でも`do`を使う: Maybe編

コマンドラインから受け取った名前に基づいて、単純なデータベースを検索するコマンドを作りましょう。

1. コマンドライン引数のリストを取得します。
2. コマンドライン引数のリストの長さが
    1. 「1」であれば、その要素を用いて、後述する4つの連想配列から対応する要素を検索します。
        1. 4つの連想配列**すべて**に該当する要素があった場合、それらをすべて標準出力に出力します （出力形式は実行例から推測してください）。
        2. それ以外の場合、「`Not found. He/She may be shy.`」と出力します。
    2. それ以外の場合は、「`Invalid arguments: <コマンドライン引数のリスト>`」という形式のメッセージを例外としてスローし、終了します。

## 検索対象となる4つの連想配列

※[「なんちゃって個人情報」][1]というサービスで生成しました。

[1]: http://kazina.com/dummy/

好きな果物

```haskell
  [ ("aki"      , "apple")
  , ("asami"    , "orange")
  , ("ouga"     , "banana")
  , ("kazuyo"   , "peach")
  , ("kazuo"    , "grape")
  , ("kimiko"   , "apple")
  , ("ken"      , "kaki")
  , ("saki"     , "pineapple")
  , ("shunsuke" , "grape")
  , ("sousuke"  , "pear")
  , ("teppei"   , "liche")
  , ("natsumi"  , "orange")
  , ("masashi"  , "peach")
  , ("meibi"    , "melon")
  , ("yu"       , "grape fruit")
  , ("yuji"     , "apple")
  , ("yoshimasa", "banana")
  , ("rio"      , "banana")
  ]
```

生年月日

```haskell
  [ ("aki"      , "1968/1/18")
  , ("asami"    , "1995/6/5")
  , ("ouga"     , "1995/9/14")
  , ("kazuyo"   , "1952/4/26")
  , ("kazuo"    , "1942/11/4")
  , ("kimiaki"  , "1970/5/17")
  , ("kimiko"   , "1988/7/31")
  , ("saki"     , "1990/3/26")
  , ("shunsuke" , "1960/11/15")
  , ("sousuke"  , "1977/1/1")
  , ("teppei"   , "1976/12/4")
  , ("natsumi"  , "1988/4/16")
  , ("masashi"  , "1954/1/28")
  , ("meibi"    , "1953/11/7")
  , ("yu"       , "1990/6/22")
  , ("yuji"     , "1989/4/16")
  , ("yoshimasa", "1992/12/6")
  , ("rio"      , "1987/12/8")
  ]
```

メールアドレス

```haskell
  [ ("aki"      , "yamane_aki@example.com")
  , ("asami"    , "ooizumi_asami@example.com")
  , ("ouga"     , "nasu_ouga@example.com")
  , ("kazuyo"   , "ezaki_kazuyo@example.com")
  , ("kimiaki"  , "tsutsui_kimiaki@example.com")
  , ("kimiko"   , "maeda_kimiko@example.com")
  , ("ken"      , "mita_ken@example.com")
  , ("saki"     , "okudera_saki@example.com")
  , ("shunsuke" , "konuma_shunsuke@example.com")
  , ("sousuke"  , "asari_sousuke@example.com")
  , ("teppei"   , "taniguchi_teppei@example.com")
  , ("natsumi"  , "komachi_natsumi@example.com")
  , ("masashi"  , "itano_masashi@example.com")
  , ("meibi"    , "shimizu_meibi@example.com")
  , ("yu"       , "matsushima_yu@example.com")
  , ("yoshimasa", "onoda_yoshimasa@example.com")
  , ("rio"      , "tsukahara_rio@example.com")
  ]
```

住んでいる都道府県

```haskell
  [ ("aki"      , "Fukuoka")
  , ("asami"    , "Ibaraki")
  , ("ouga"     , "Tochigi")
  , ("kazuyo"   , "Kanagawa")
  , ("kazuo"    , "Kanagawa")
  , ("kimiaki"  , "Okayama")
  , ("kimiko"   , "Gunma")
  , ("ken"      , "Fukuoka")
  , ("saki"     , "Fukui")
  , ("shunsuke" , "Tochigi")
  , ("sousuke"  , "Wakayama")
  , ("teppei"   , "Kanagawa")
  , ("natsumi"  , "Tochigi")
  , ("masashi"  , "Aomori")
  , ("meibi"    , "Tochigi")
  , ("yu"       , "Tokyo")
  , ("yoshimasa", "Mie")
  , ("rio"      , "Hokkaido")
  ]
```

## 実行例

すべての連想配列に対応する要素が存在する名前を指定したとき

```shell
shell> stack exec runhaskell 21.hs aki
fruit: apple
birthday: 1968/1/18
mailAddress: yamane_aki@example.com
prefecture: 福岡県
```

いずれの連想配列にも存在しない名前を指定したとき

```shell
> stack exec runhaskell 21.hs none
Not found. He/She might be shy.
```

「誕生日」の連想配列に対応する要素が存在しない名前を指定したとき

```shell
> stack exec runhaskell 21.hs ken
Not found. He/She might be shy.
```

## 必要な知識

### 問題提起: `case`式を何度も書くのが面倒くさい

今回の課題は、（ここ最近のいくつかの課題と同様）新しい知識を学ばなくても解くことができます。  
しかし、「4つの連想配列**すべて**に該当する要素があった場合」を愚直に実装しようとすると、結構面倒なことになります。

`lookup`関数が返すのは`Maybe`型の値

```haskell
ghci> import qualified Data.Map.Strict as M
ghci> :t M.lookup
M.lookup :: Ord k => k -> M.Map k a -> Maybe a
```

なので`Maybe`型を扱うために`case`式を扱う必要がある（課題9を思い出しましょう）

例えば以下のような`Maybe`な値があったとして、

```haskell
ghci> ma = Just 'a'
ghci> mb = Just 'b'
ghci> mc = Just 'c'
```

愚直に`case`式を使うと...

```haskell
ghci> :{
ghci| case ma of
ghci|   Just a ->
ghci|     case mb of
ghci|       Just b ->
ghci|         case mc of
ghci|           Just c ->
ghci|             putStrLn $ "Found " ++ show a ++ show b ++ show c
ghci|           Nothing ->
ghci|             putStrLn "Not found."
ghci|       Nothing ->
ghci|         putStrLn "Not found."
ghci|   Nothing ->
ghci|     putStrLn "Not found."
ghci| :}
Found 'a''b''c'
```

ネストがめっちゃ深い！

これまでの課題の知識を少しひねって使うなら、↓のように一旦タプルに包んでもよい

```haskell
ghci> :{
ghci| case (ma, mb, mc) of
ghci|   (Just a, Just b, Just c) ->
ghci|     putStrLn $ "Found " ++ show a ++ show b ++ show c
ghci|   _ ->
ghci|     putStrLn "Not found."
ghci| :}
Found 'a''b''c'
```

今回は、一旦タプルに入れる必要がない、より汎用性が高い方法を紹介します。

### `Maybe`に対して`<$>`を使う

普通に関数を適用しようとしても`Maybe`型の値を引数としてとらない関数である限りは当然エラーに。

```haskell
ghci> ma = Just 'a'

ghci> import Data.Char
ghci> toUpper ma

<interactive>:59:9: error:
    ? Couldn't match expected type ‘Char’ with actual type ‘Maybe Char’
    ? In the first argument of ‘toUpper’, namely ‘ma’
      In the expression: toUpper ma
      In an equation for ‘it’: it = toUpper ma
```

下記のように、`Maybe`型の値が`Just`か`Nothing`か確認して、`Just`だったら関数を適用しつつ、`Nothing`だったら何もしない、といった処理は、割とありふれている

```haskell
case ma of
  Just a -> Just $ toUpper a
  Nothing -> Nothing
```

Haskellは「関数を受け取る関数」も簡単に作れるので、↓のような関数を定義できる

```haskell
applyIfJust :: (a -> b) -> Maybe a -> Maybe b
applyIfJust f mx =
  case mx of
      Just a -> Just $ f a
      Nothing -> Nothing
```

```haskell
ghci> applyIfJust toUpper ma
Just 'A'
```

そしてもちろんすでにそれはある。  
それが`<$>`！

```haskell
ghci> toUpper <$> ma
Just 'A'
```

`Maybe`に対する`<$>`は、前述の`applyIfJust`と同様、

- `Just`であれば第1引数（左辺）に渡した関数を実行し、結果をまた`Just`でくるんで返し、
- `Nothing`であればそのまま`Nothing`を返す、

という動作になっております。

```haskell
ghci> toUpper <$> Just 'z'
Just 'Z'
ghci> toUpper <$> Nothing
Nothing
```

一言で言うと、`Maybe`に対する`<$>`は、「**右辺が`Just`か`Nothing`か確認**して関数を適用する」関数と言える

当然型も`applyIfJust`と同じ

```haskell
ghci> :set -XTypeApplications
ghci> :t (<$>) @Maybe
(<$>) @Maybe :: (a -> b) -> Maybe a -> Maybe b
```

### `Maybe`に対して`<*>`を使う

今回の課題のように2つ以上の`Maybe`な値があって、それを（`a -> b -> c`という型の）複数の引数を受け取る関数に渡す場合

```haskell
ghci> (++) <$> Just "foo" <*> Just "bar"
Just "foobar"
```

どれか一つでも`Nothing`を渡すと、`<*>`も`Nothing`を返す。

```haskell
ghci> (++) <$> Nothing <*> Just "bar"
Nothing
ghci> (++) <$> Just "foo" <*> Nothing
Nothing
```

型を覗いてみましょう。

```haskell
ghci> :set -XTypeApplications
ghci> :t (<*>) @Maybe
(<*>) @Maybe :: Maybe (a -> b) -> Maybe a -> Maybe b
```

案の定、`Maybe (a -> b)`と`Maybe a`を受け取ることがわかる

より詳しく動作を確認するために、`<$>`を使わずに`Maybe (a -> b)`な値を作ってみよう

```haskell
ghci> :t Just (* 2)
Just (* 2) :: Num a => Maybe (a -> a)
```

やはり、左辺か右辺どちらか一方でも`Nothing`を渡すと、そのまま`Nothing`を返す

```haskell
ghci> Just (* 2) <*> Just 5
Just 10
ghci> Nothing <*> Just 5
Nothing
ghci> Just (* 2) <*> Nothing
Nothing
```

このことから、`Maybe`に対する`<*>`は、「**左辺と右辺それぞれを続けて`Just`か`Nothing`か確認**して関数を適用する」関数と言える

### `Maybe`に対して`do`や`>>=`を使う

`Maybe`に対しても`>>=`や`=<<`は使える

今度は型から見てみよう

```haskell
ghci> :set -XTypeApplications
ghci> :t (=<<) @Maybe
(=<<) @Maybe :: (a -> Maybe b) -> Maybe a -> Maybe b
```

`(a -> Maybe b)`な関数と、`Maybe a`な値を受け取る

`(a -> Maybe b)`の典型例といえば、課題17で紹介した[`Data.Map.Strict`モジュールの`lookup`関数](http://hackage.haskell.org/package/containers-0.6.2.1/docs/Data-Map-Strict.html#v:lookup)！

```haskell
ghci> import qualified Data.Map.Strict as M
ghci> :t M.lookup
M.lookup :: Ord k => k -> M.Map k a -> Maybe a
```

第1引数に連想配列のkeyとなる値を渡せば、後は`(a -> Maybe b)`という形の関数になる！

```haskell
ghci> :t M.lookup "key"
M.lookup "key" :: M.Map [Char] a -> Maybe a
```

引数を入れ替えても`(a -> Maybe b)`になる！

```haskell
ghci> :t (\m key -> M.lookup key m) M.empty
(\m key -> M.lookup key m) M.empty :: Ord k => k -> Maybe a
```

よくある例: `lookup`して得た結果で`lookup`したい

名前から好きな果物を引いて、果物の値段を調べる

```haskell
fruitDictionary :: M.Map String String
fruitDictionary = M.fromList
  ... -- 「検索対象となる4つの連想配列」の「好きな果物」からコピペしてください
```

```haskell
ghci> :{
ghci| priceDictionary :: M.Map String Int
ghci| priceDictionary = M.fromList
ghci|   [ ("orange"     , 98)
ghci|   , ("apple"      , 198)
ghci|   , ("grape fruit", 138)
ghci|   , ("banana"     , 258)
ghci|   , ("pineapple"  , 458)
ghci|   , ("grape"      , 398)
ghci|   , ("melon"      , 4980)
ghci|   , ("kaki"       , 198)
ghci|   , ("peach"      , 298)
ghci|   , ("pear"       , 178)
ghci|   ]
ghci| :}
```

```haskell
ghci> M.lookup "rio" fruitDictionary
Just "banana"
ghci> M.lookup "rio" fruitDictionary >>= (\fruit -> M.lookup fruit priceDictionary)
Just 258
```

liche（ライチ）は`priceDictionary`に登録していないので、teppeiさんが好きな果物の価格を調べようとしても、`Nothing`が返ってきます。

```haskell
ghci> M.lookup "teppei" fruitDictionary
Just "liche"
ghci> M.lookup "teppei" fruitDictionary >>= (\fruit -> M.lookup fruit priceDictionary)
Nothing
```

「`M.lookup`して得た結果に基づいて別の連想配列を`M.lookup`する」という処理を簡単に書けるのが伝わったでしょうか。  
さらにこれを一般化していうと、「`Maybe a`を返す関数、つまり『結果として`Just`を返したか`Nothing`を返したか確認する必要がある関数』の**結果に基づいて**、また別の『結果として`Just`を返したか`Nothing`を返したか確認する必要がある関数』を**続けて実行**するのに便利な関数」、それが`Maybe`に対する`>>=`なのです。

`>>=`や`=<<`が使えると言うことは、`do`も使える！  
さっきの例を`do`で書き換えてみましょう。

```haskell
ghci> :{
ghci| do
ghci|   fruit <- M.lookup "rio" fruitDictionary
ghci|   M.lookup fruit priceDictionary
ghci| :}
Just 258
```

```haskell
ghci> :{
ghci| do
ghci|   fruit <- M.lookup "teppei" fruitDictionary
ghci|   M.lookup fruit priceDictionary
ghci| :}
Nothing
```

やっぱり`>>=`を使った場合と同じ結果になりましたね！  
このように`do`における`<-`や`<$>`、`<*>`に`>>=`といった演算子は、これまで何度も使ってきた`IO`（命令）だけでなく、`Maybe`に対して使えます。  
これが一体どういうことなのか、次の節でより詳しく説明しましょう。

### 「命令」としての`Maybe`

`Maybe`は`Functor`、`Applicative`、`Monad`のインスタンス

```haskell
ghci> :i Maybe
data Maybe a = Nothing | Just a         -- Defined in ‘GHC.Maybe’
instance Applicative Maybe -- Defined in ‘GHC.Base’
instance Eq a => Eq (Maybe a) -- Defined in ‘GHC.Maybe’
instance Functor Maybe -- Defined in ‘GHC.Base’
instance Monad Maybe -- Defined in ‘GHC.Base’
-- ... 以下略
```

`<$>`や`<*>`、`>>=`や`do`が使えるのは、`IO`も`Maybe`も、これら3つの型クラスのインスタンスであるためです。

hoge

### 課題のヒント

`<$>`も`<*>`も`>>=`も、最終的に返す値は`Maybe`な値であることに注意。  
`<$>`や`<*>`、`>>=`だけでは今回の課題は解けず、結果を利用するために必ず一度は`case`を使う必要があります。
