# 入力の取得

標準入力から読んだ文字列を、改行文字で区切って、逆順に並び替えて標準出力に書き込みましょう。

## 実行結果例

`input.txt`という、次のような内容のファイルがあるとき、

```
Lorem ipsum dolor sit amet,
sed do eiusmod tempor incididunt ut labore et dolore magna aliqua.
consectetur adipiscing elit,
```

下👇のように実行すると、

```
shell> stack exec runhaskell 3.hs < input.txt
```

下👇のような文字列を出力します。

```
sed do eiusmod tempor incididunt ut labore et dolore magna aliqua.
consectetur adipiscing elit,
Lorem ipsum dolor sit amet,
```

## 必要な知識

<!-- 高階関数に触れてないので interact はなしで。 -->

### リスト、`reverse`関数、`lines`関数、`unlines`関数、`putStr`関数

まずは入力や出力をどのように行うのかは忘れて、「文字列に対して、どんな処理をするのか」を考えましょう。  
今回の課題に関して言えばずばり「文字列を、改行文字で区切って、逆順に並び替え」る処理が該当します。

ちょっと脱線。  
Haskellでのプログラミングに限らず、プログラムの設計について考えるときは、可能な限り「入力や出力に関わる部分」と「<small>（入力を受け取ってできた）</small>**値に対して**<small>（最終的に出力する前に）</small>**どのような処理をするのか**」を分けて考えると、より再利用性が高く、テストも理解もしやすいプログラムが書けます。  
例えば今回の課題に関して言えば、そのように考えることで、「文字列を改行文字で区切って、逆順に並び替え」る処理を、GHCi上でもお気軽にテストできます。

と、言うわけで今回の課題を解くための関数をGHCi上で試しましょう。

```bash
shell> stack exec ghci
```

最初に紹介するのは文字列を「改行文字で区切る」関数、`lines`関数です。  
課題3で触れた、`\n`を思い出しながら、次のように入力してみてください。

```haskell
ghci> lines "A string\ncontaining\nnewlines\nin the middle."
["A string","containing","newlines","in the middle."]
```

出力された角括弧`[]`で囲われたものは、文字列のリストです。  
リストは、Haskellで（同じ型の）複数の値を表すのに使われる、最も単純なデータ構造です。  
もちろん文字列だけでなく、ほかのどんな型の値も入れることができます。  
例えば下記は数値のリストです。

```haskell
ghci> [1, 2, 3]
[1,2,3]
````

`lines`関数は、受け取った文字列を改行文字(`\n`)で区切ることで、その文字列のリストを作ります。  
これで「改行文字で区切って」という処理は実装できますね。

続いて、「逆順に並び替え」る処理です。  
`lines`関数で作った文字列のリストを逆順にするには、文字通り`reverse`関数を使いましょう。

```haskell
ghci> reverse ["A string","containing","newlines","in the middle."]
["in the middle.","newlines","containing","A string"]
```

逆順になって返ってきましたね。

さて、この課題の要件では最終的に「標準出力に書き込」まなければならないのでした。  
これまでの課題で勉強した`putStrLn`関数は、文字列を渡さないとエラーになります。

```haskell
ghci> putStrLn ["in the middle.","newlines","containing","A string"]
> putStrLn ["in the middle.","newlines","containing","A string"]

<interactive>:12:11: error:
    ? Couldn't match expected type ‘Char’ with actual type ‘[Char]’
    ? In the expression: "in the middle."
      In the first argument of ‘putStrLn’, namely
        ‘["in the middle.", "newlines", "containing", "A string"]’
      In the expression:
        putStrLn ["in the middle.", "newlines", "containing", "A string"]

... （以下略）
```

課題2で紹介した`print`関数は文字列のリストにも使えます。

```haskell
ghci> print ["in the middle.","newlines","containing","A string"]
["in the middle.","newlines","containing","A string"]
```

ただし、これは課題の要件にあった出力ではありません。  
改行文字で区切って逆順にした後、各行を逆順に出力するには、また改行文字で文字列を結合する必要があるのです。  
そこで役に立つのが`unlines`関数です。

```haskell
ghci> unlines ["A string","containing","newlines","in the middle."]
"A string\ncontaining\nnewlines\nin the middle.\n"
```

期待したとおり、リストの要素が改行文字で結合されて、一つの文字列に変わったのがわかるでしょうか？  
できた文字列を`putStrLn`してみると、よくわかります。

```haskell
ghci> putStrLn "A string\ncontaining\nnewlines\nin the middle.\n"
A string
containing
newlines
in the middle.

ghci>
```

おっと、最後の行`in the middle`の後に、余計な空行ができてしまいました。  
これでは課題の要件をクリアできません。  
`putStrLn`の`Ln`は「Line」の略であるとおり、必ず行の末尾の文字である`\n`を出力します。  
そして、`unlines`も文字列の末尾に`\n`を加えます。  
`\n`はあくまでも行の末尾にあるべき文字なので、最後の文字にもちゃんと加えるのでしょう。

いずれにしても、この問題を回避するためには、`putStr`という文字通り`Ln`がつかない関数を使います。

```haskell
ghci> putStr "A string\ncontaining\nnewlines\nin the middle.\n"
A string
containing
newlines
in the middle.
ghci>
```

今度は余計な空行が出てませんね！LGTM！

### 全部組み合わせる

さて、ここまで紹介した関数を生かして、「文字列を、改行文字で区切って、逆順に並び替え」る処理と、さらにそれを出力する処理を実装してみましょう。  
全部組み合わせてしまうと課題の答えになってしまうので、一部の処理を示すことで、注意すべき点をお伝えしたいと思います。

結論から先に書くと、文字列を改行文字`\n`で区切って`print`関数で中身を表示する処理は、次のように書くことで実装できます。

```haskell
ghci> print (lines "A string\ncontaining\nnewlines\nin the middle.\n")
["A string","containing","newlines","in the middle."]
```

`(lines "A string\ncontaining\nnewlines\nin the middle.\n")`と、**関数名とその引数両方をカッコで**囲っている点に注意してください。  
ほかのよくあるプログラミング言語のように、

```haskell
ghci> print lines("A string\ncontaining\nnewlines\nin the middle.\n")
```

でもなければ、ましてや

```haskell
ghci> print lines "A string\ncontaining\nnewlines\nin the middle.\n"
```

でもない点に注意してください。  
この場合は、`print`関数に`lines`関数と`"A string\ncontaining\nnewlines\nin the middle.\n"`という**2つの引数**を渡している、という意味になります。  
「`print`関数に`lines`関数を渡す」というのがちょっとおかしく聞こえるかもしれません。  
`print`関数以外の関数ですと、Haskellの構文上そういう式も普通にあり得るので、Haskellは2つの引数を渡した、と解釈します。

いずれにしても、`print`関数は1つの引数しか受け取らないので、上記の式はエラーになります。

```haskell
ghci> print lines "A string\ncontaining\nnewlines\nin the middle.\n"

<interactive>:23:1: error:
    ? Couldn't match expected type ‘[Char] -> t’
                  with actual type ‘IO ()’
    ? The function ‘print’ is applied to two arguments,
      but its type ‘(String -> [String]) -> IO ()’ has only one
      In the expression:
        print lines "A string\ncontaining\nnewlines\nin the middle.\n"
      In an equation for ‘it’:
          it = print lines "A string\ncontaining\nnewlines\nin the middle.\n"
    ? Relevant bindings include it :: t (bound at <interactive>:23:1)
```

`? The function ‘print’ is applied to two arguments,`  
`but its type ‘(String -> [String]) -> IO ()’ has only one` という箇所が該当しますので、ぼんやり覚えておいてください。

ここまでの話を一般化しましょう。  
Haskellで関数を呼び出す際は、次のような形式で書いてください。

```haskell
関数名 引数1 引数2 ... 引数N
```

関数名と引数をスペースで区切っているだけでいいのです。  
カッコは、どの関数にどの引数を渡しているのか、明確にしたいときだけ使います。

```haskell
関数A Aの引数1 (関数B Bの引数1 Bの引数2 ... Bの引数N) Aの引数3 ... Aの引数N
--             ^------------------------------------^
--                  この部分が関数Aの引数2になる
```

関数呼び出しは非常に頻繁に使う構文なので、簡単に使えるのはいいことですね！

ちなみに、Haskellにはこうした関数呼び出しがもっと複雑に入れ子になった場合に、より簡潔に書く方法があります。  
おいおい説明しますね。

### `do`記法で`getContents`関数と細い左矢印`<-`を使って、入力を受け取る

いよいよ、標準入力から文字列を読んで、変数に代入する方法を説明しましょう。  
Haskellでこれを一番簡単に実現する方法は、ちょっと変わっています。  
例えば、「`getContents`という命令を使って標準入力から読んだ文字列を、`input`という変数に代入する」という処理は、下記のように書きます。

```haskell
do
  input <- getContents
```

変数への代入というと、イコール `=` を使った構文をイメージする方が多いかと思います。  
しかし、ここでは細い左矢印 `<-` を使わなければなりません<small>（まだ詳しく説明していませんが、イコール `=`を使った代入もやっぱりあります）</small>。

Haskellにおいて、`getContents`は、「命令<small>（慣習的に『アクション』と呼ばれることも多いです）</small>」であり、その実行結果を取り出して変数に代入するには、細い左矢印 `<-` を使います。

これまでに何度も使った、`putStrLn`や`print`、それから`putStr`も「命令」です。  
ただし、`putStrLn`や`print`などは特に意味のある結果を返さないので、これまで`<-`は出てきませんでした。  
`do`記法は、これらのような「命令」を列挙したり、「命令」の実行結果を簡単に利用するための仕組みを提供してくれるのです。

細い左矢印 `<-` を使って代入した変数は、普通の変数と同様に参照して使うことができます。

```haskell
do
  input <- getContents
  putStr input
```

このように書くと、おなじみ`cat`コマンドのように、「標準入力から文字列を読んで、標準出力にそのまま書き込む」ことができます。

おなじみ`main`の定義に書けば、簡易版`cat`コマンドのできあがりです！

```haskell
main = do
  input <- getContents
  putStr input
```

ただし、**細い左矢印 `<-` は`do`の中でしか使えない**、という点に注意してください。

次のプログラムを実行しようとすると、エラーになってしまいます。

```haskell
main =
  input <- getContents
  putStr input
```

```
shell> stack exec runhaskell no-do.hs
no-do.hs:2:9: error:
    parse error on input ‘<-’
    Perhaps this statement should be within a 'do' block?
  |
2 |   input <- getContents
  |         ^^
```

`Perhaps this statement should be within a 'do' block?`というエラーメッセージは、わざわざ「この命令<small>(`input <- getContents`)</small>は`do`ブロックの中で使うべきなのでは？」と提案してきてくれています。  
「わかってるなら補ってくれてもいいのに...」という気もしますが、気を取り直して`do`を付け足しましょう。

## 課題の解き方

`getContents`で取得した標準入力の文字列を、細い左矢印 `<-` で取り出し、適当な変数に代入してください。  
その代入した変数に対して、「改行文字で区切って、逆順に並び替え」る関数を順番に適用しましょう。  
最後は逆順になったリストを改行文字で結合し直し、`putStr`で出力すればバッチリです。  
あっ、すべて`do`記法の中に書くのをお忘れなく。

## コラム: 「代入」か「束縛」か

ところで、読者の皆さんの中には、「Haskellでは『代入 (assignment)』という言葉は使わないよ。『束縛 (binding)』だよ」という主張を聞いたことがあるかも知れません（[例えばこれ][1]）。  
この主張は正しいです。  
`input <- getContents`という行は、「`input`という変数に`getContents`の結果を**束縛する**」と説明した方が本来は適切です。  
しかしこの当入門「失敗しながら学ぶHaskell入門」では、「理論的な背景の違いはあるものの、少なくともソースコードを書いて実行するだけの我々からみれば、実用上の違いはほとんどない」という立場から、読者にとってより親しみの深いであろう「代入」という単語を敢えて使っています。

[1]: https://qiita.com/lotz/items/aca1d179c14d4dca5099 "Haskellの変数には値を代入しません、束縛します。"

とはいえ、違いを知っておくことによって、Haskellのことをより深く理解できるので[神戸大学のとある講義での解説][2]や[こちら][3]を参考に、ここでも違いを明確にしておきましょう。

[2]: https://www.fine.cs.kobe-u.ac.jp/members/kamada/old/scheme_web/let_1_2.html "手続き型との違い"
[3]: https://tarao.hatenablog.com/entry/20100208/1265605429 "ラムダ計算基礎文法最速マスター"

Haskellをはじめとする、いわゆる「関数型プログラミング言語」と呼ばれるものは通常、「ラムダ計算」というプログラミング言語のモデルを元に作られています。  
「ラムダ計算」の世界には「変数」という概念が出てくるものの、それらはすべて**関数の引数として渡す**ことでしか値を割り当てることができません。  
そうした、「関数の引数として渡す」ことで**「変数」に値を直接結びつける操作を「束縛」**と言います。  
これはHaskellでも同じです。  
Haskell（と、ラムダ計算）において、あらゆる変数は、値を関数の引数として受け取ることで、割り当てられているのです。  
`input <- getContents`という行は、私たちが親しみやすい構文で、うまいことそれを隠しているに過ぎません。糖衣構文というやつですね。

疑似言語で例えると、下記👇のようなイメージになります。

```javascript
function useInput(input) {
  /* ここで、input という名前で束縛された引数を使用する。 */
  /* 例えば、putStrLnしたりする。 */
}

function main() {
  /* あらゆる変数は関数の引数として渡すことで初めて「束縛」されるので、 */
  /* ここには変数は一切現れず、getContentsの結果はただuseInputに渡されるだけ。 */
  useInput(getContents());
}
```

一方、変数に値を「代入」する際、変数によって名前が割り当てられるのは、値そのものではありません。  
**値を入れる「場所」**（コンピューターから見た世界観で言うと「アドレス」）なのです。  
つまり、「代入」という操作は値を入れる「場所」に変数を割り当てた上で、その「場所」に値をセットする、という操作なのです。  
「束縛」が変数を「値そのもの」に対して結びつける操作なのに対して、「代入」は「値を入れる場所」に変数を結びつけた上で値をセットする、といった具合に、変数を結びつける対象が異なる、という点をご理解いただけたでしょうか？

「代入は同じ名前の変数に再度割り当てること（再代入）が可能で、束縛はそれができない」という区別で理解している方もいらっしゃるようですが、これはあまり正確ではありません。  
というのも、「束縛」の場合も下記のようなコードが普通に書けてしまうからです（`getLine`については次の課題で教えます）。

```haskell
main = do
  input <- getLine
  input <- getLine
  putStrLn input
```

このコードは見かけから想像されるとおり、2行目の`input <- getLine`が優先され、ユーザーが2行目に入力した文字列を`putStrLn`で出力します。  
`input`という変数に、`getLine`の結果を2回割り当てているように見えますよね？

実際には、これは「再束縛」などと呼ばれるものではなく、「シャドーイング (shadowing)」と呼ばれる操作です。  
ラムダ計算の言葉で言うと、「同じ名前の変数を受け取る関数」を一方の内側に作ってしまい、外側の関数で参照している変数名に「覆いかぶせてしまう」ような操作を指しています。
これも疑似言語で、上記👆のコードを書き換えることで例えてみましょう。

```javascript
function useInput1(input) {
  /* この外側の関数の引数 input に加えて ... */

  function useInput2(input) {
    /* ... この内側の関数にも引数 input がある！ */
    putStrLn(input)
  }

  /* そして、useInput2に渡す値は、 */
  /* 外側の関数の引数 input ではない。*/
  /* なので当然内側の関数の引数 input が使われ、*/
  /* 外側の関数の引数 input は、内側の関数の引数に覆い被さられてしまうような格好になる。*/
  useInput2(getLine());
}

function main() {
  useInput1(getLine());
}
```

ちなみに、警告を有効にしていれば先ほどのHaskellのコードには警告が出ます。  
一般に、このようにシャドーイングすることは、変数を思わぬ値に書き換えてしまうことに繋がるので、推奨されていません。  
もろもろの事情により当入門では意図的に警告を有効にしていませんが、実際にHaskellを本格的に使うようになったら警告は必ず有効にしましょう（やり方はその時また）。

なお、この問題についての詳細は[こちらの議論][issue28]もご覧ください。

[issue28]: https://github.com/haskell-jp/makeMistakesToLearnHaskell/issues/28 "再代入ができないことや「束縛」という単語について触れる #28"
