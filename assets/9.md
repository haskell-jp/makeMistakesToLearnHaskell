# 入力した値の内容に応じて処理を分ける

標準入力から空白で区切られた身長・体重を1行で受け取り、BMIを計算するプログラムを書きましょう。

1. プロンプトとして「`Height Weight: `」と表示します。
2. 標準入力から1行の入力を受け取ります。
3. 1行目の入力を、空白で区切った文字列のリストとして代入します。
4. 代入した文字列のリストの要素数が
    1. 「2以上」であれば、1つめの要素を身長、2つめの要素を体重が書かれた文字列として解釈して、BMIを計算し、標準出力に出力してください。
    2. 「1」であれば、下記の処理を行います。
        1. プロンプトとして「`Weight: `」と表示します。
        2. 標準入力から1行の入力を受け取ります。
        3. 最初に入力した行を身長、2行目に入力した行を体重が書かれた文字列として解釈して、BMIを計算し、標準出力に出力してください。
    3. それ以外の場合は、「`Invalid input: <入力した文字列>`」という形式のメッセージを標準出力に出力してください。

## 注意事項

- バッファリングの都合により、「プロンプト」は**`putStr`はなく`putStrLn`で出力**してください。  
  ちゃんとしたバッファリングの設定はまたいつか！
- 小数点以下の値に対して、四捨五入などの丸め処理をする必要はありません。
- 浮動小数点数の丸め誤差を気にする必要もありません。

## 実行例

身長として「`2`」、体重として「`80`」を、入力した場合（身長、体重ともに1行目に入力している）:

```bash
shell> stack exec runhaskell ./assets/9.hs
Height Weight:
2 80
20.0
```

身長として「`2`」、体重として「`80`」を、入力した場合（身長と体重を別々の行に入力している）:

```bash
shell> stack exec runhaskell ./assets/9.hs
Height Weight:
2
Weight:
80
20.0
```

3つの単語を入力した場合

```bash
shell> stack exec runhaskell ./assets/9.hs
Height Weight:
2 3 4
Invalid input: 2 3 4
```

何も入力しなかった場合

```
shell> stack exec runhaskell ./assets/9.hs
Height Weight:

Invalid input:
```

## 必要な知識

### 計算式

課題2でも同じ式を載せましたが、改めて一般化して掲載します。

身長`H`m, 体重`W`kgの人のBMIは、以下の計算式で求めることができます。

```
  W  ÷ (  H  ×   H )
^^^^    ^^^^   ^^^^
体重 ÷ (身長 × 身長)
```

### いろいろなデータ構造に対するパターンマッチ

#### 復習 タプルに対するパターンマッチ

```haskell
ghci> (x, y) = ("aa", "bb")
ghci> x
"aa"
ghci> y
"bb"
```

#### リストに対するパターンマッチ（リストの長さがわかっている場合）

```haskell
ghci> [x, y] = ["aa", "bb"]
ghci> x
"aa"
ghci> y
"bb"
```

マッチさせる要素の数と実際の要素の数が食い違っていた場合エラーになる！

```haskell
ghci> [x, y, z] = ["aa", "bb"]
ghci> x
"*** Exception: <interactive>:75:1-24: Irrefutable pattern failed for pattern [x, y, z]

ghci> y
"*** Exception: <interactive>:75:1-24: Irrefutable pattern failed for pattern [x, y, z]

ghci> z
"*** Exception: <interactive>:75:1-24: Irrefutable pattern failed for pattern [x, y, z]

ghci>
```

ちょっと遠回り

#### レコードに対するパターンマッチ

レコードに対するパターンマッチ1

```haskell
ghci> :{
ghci| data Entry =
ghci|   Entry
ghci|   { category :: String
ghci|   , price :: Integer
ghci|   } deriving Show
ghci| :}
ghci> entry1 = Entry "book" 2980
ghci> Entry cat pri = entry1
ghci> cat
"book"
ghci> pri
2980
```

レコードに対するパターンマッチ2

```haskell
ghci> entry2 = Entry { category = "glass", price = 12980 }
ghci> Entry { category = cat, price = pri } = entry2
ghci> cat
"glass"
ghci> pri
12980
```

#### 組み合わせたパターンマッチ

実際に余り複雑に入れ子になったものをすることはないし、やめた方がいい。

```haskell
ghci> :{
ghci| data DatedEntries =
ghci|   DatedEntries
ghci|   { entries :: [Entry]
ghci|   , dateYMD :: (Integer, Integer, Integer)
ghci|   } deriving Show
ghci| :}
ghci> todayEntries = DatedEntries [entry1, entry2] (2019, 2, 27)
ghci> DatedEntries [firstEntry, lastEntry] (y, m, d) = todayEntries

ghci> entry1
Entry {category = "book", price = 2980}
ghci> firstEntry
Entry {category = "book", price = 2980}

ghci> entry2
Entry {category = "glass", price = 12980}
ghci> lastEntry
Entry {category = "glass", price = 12980}

ghci> y
2019
ghci> m
2
ghci> d
27
```

やり過ぎ注意。

- 値コンストラクターを`=`の右側に書くと、値を作るのに使える。
- 値コンストラクターを`=`の左側に書くと、値をパターンマッチで分解するのに使える。

#### リストの本当の値コンストラクター

リストの長さにとらわれない、本物の値コンストラクター

```haskell
ghci> 1 : []
[1]
ghci> 1 : 2 : []
[1,2]
ghci> 1 : 2 : 3 : []
[1,2,3]
```

```haskell
ghci> 1 : (2 : (3 : []))
[1,2,3]
```

空リストに対して、1個ずつ要素を重ねたような構造になっています。  
「先頭の要素と残りのリスト」を持った構造体です。  
データ構造に詳しい人向けに言い換えるとリンクリストです。

間違っても下記のように結合しているわけではありません。

```haskell
ghci> ((True : False) : True) : []

<interactive>:87:10: error:
    • Couldn't match expected type ‘[Bool]’ with actual type ‘Bool’
    • In the second argument of ‘(:)’, namely ‘False’
      In the first argument of ‘(:)’, namely ‘(True : False)’
      In the first argument of ‘(:)’, namely ‘((True : False) : True)’

<interactive>:87:19: error:
    • Couldn't match expected type ‘[[Bool]]’ with actual type ‘Bool’
    • In the second argument of ‘(:)’, namely ‘True’
      In the first argument of ‘(:)’, namely ‘((True : False) : True)’
      In the expression: ((True : False) : True) : []
ghci>
```

`:` は**リストの先頭の要素と、残りのリストを受け取る値コンストラクター**です。
`[1]`や`[1, 2]`も、本当は`1 : []`や`1 : 2 : []`の糖衣構文でしかありません。

#### リストに対するパターンマッチ（リストの長さがわからない場合）

```haskell
ghci> x : xs = [1, 2, 3]
ghci> x
1
ghci> xs
[2,3]

ghci> y : ys = xs
ghci> y
2
ghci> ys
[3]
```

```haskell
ghci> x : y : xs = [1, 2, 3]
ghci> x
1
ghci> y
2
ghci> xs
[3]
```

型注釈の`::`と間違えないように気をつけてください！

`:`を使ってもうまく扱えないケースが！

```haskell
ghci> x : xs = []
ghci> x
*** Exception: <interactive>:57:1-11: Irrefutable pattern failed for pattern x : xs

ghci> xs
*** Exception: <interactive>:57:1-11: Irrefutable pattern failed for pattern x : xs
```

### `case`式で場合分けされたパターンマッチをする

```haskell
ghci> possiblyEmpty = []
ghci> :{
ghci| case possiblyEmpty of
ghci|   x : xs -> print x
ghci|   [] -> putStrLn "Empty!"
ghci| :}
Empty!
```

```haskell
ghci> possiblyEmpty = [1, 2, 3]
ghci> :{
ghci| case possiblyEmpty of
ghci|   x : xs -> print x
ghci|   [] -> putStrLn "Empty!"
ghci| :}
1
```

```haskell
ghci> xs = [1, 2, 3]
ghci> :{
ghci| case xs of
ghci|   [first, second, third] -> putStrLn "Three elements!"
ghci|   [first, second] -> putStrLn "Two elements!"
ghci|   [] -> putStrLn "Empty!"
ghci| :}
Three elements!
```

```haskell
ghci> xs = [1]
ghci> :{
ghci| case xs of
ghci|   [first, second, third] -> putStrLn "Three elements!"
ghci|   [first, second] -> putStrLn "Two elements!"
ghci|   [] -> putStrLn "Empty!"
ghci| :}
*** Exception: <interactive>:(100,1)-(103,25): Non-exhaustive patterns in case
```

```haskell
ghci> xs = [1]
ghci> :{
ghci| case xs of
ghci|   [first, second, third] -> putStrLn "Three elements!"
ghci|   [first, second] -> putStrLn "Two elements!"
ghci|   [] -> putStrLn "Empty!"
ghci|   o -> putStrLn ("Other! " ++ show o)
ghci| :}
Other! [1]
```

シャドーイングした例

```haskell
ghci> xs = [1]
ghci> :{
ghci| case xs of
ghci|   [first, second, third] -> putStrLn "Three elements!"
ghci|   [first, second] -> putStrLn "Two elements!"
ghci|   xs -> putStrLn ("Other! " ++ show xs)
ghci| :}
Other! [1]
```

```haskell
ghci> xs = [1]
ghci> :{
ghci| case xs of
ghci|   [_first, _second, _third] -> putStrLn "Three elements!"
ghci|   [_first, _second] -> putStrLn "Two elements!"
ghci|   o -> putStrLn ("Other! " ++ show o)
ghci| :}
Other! [1]
```

```haskell
ghci> xs = [1]
ghci> :{
ghci| case xs of
ghci|   [_, _, _] -> putStrLn "Three elements!"
ghci|   [_, _] -> putStrLn "Two elements!"
ghci|   o -> putStrLn ("Other! " ++ show o)
ghci| :}
Other! [1]
```

```haskell
ghci> xs = ["a"]
ghci> :{
ghci| case xs of
ghci|   x : _xs -> putStrLn ("Head: " ++ x)
ghci|   _other -> putStrLn "Other! "
ghci| :}
Head: a
```

```haskell
ghci> xs = []
ghci> :{
ghci| case xs of
ghci|   x : _xs -> putStrLn ("Head: " ++ x)
ghci|   _other -> putStrLn "Other! "
ghci| :}
Other!
```

### `case`式の中でも`do`記法を使う

```haskell
ghci> possiblyEmpty = ["a", "b", "c"]
ghci> :{
ghci| case possiblyEmpty of
ghci|   x : xs -> do
ghci|     putStrLn ("The first element is: " ++ x ++ ".")
ghci|     putStrLn ("The rest elements are: " ++ show xs ++ ".")
ghci|   [] -> putStrLn "Empty!"
ghci| :}
The first element is: a.
The rest elements are: ["b","c"].
```

```
ghci> numbers = [1, 2, 3]
ghci> :{
ghci| case numbers of
ghci|   [n1, n2] -> do
ghci|     let answer = n1 + n2
ghci|     putStrLn ("The answer is: " ++ show answer)
ghci|   other -> do
ghci|     putStrLn "Sorry, I don't know how to sum them up!"
ghci|     putStrLn ("Your input is: " ++ show other)
ghci| :}
Sorry, I don't know how to sum them up!
Your input is: [1,2,3]
```

### `words`関数で空白文字で区切られた文字列を分割する

```haskell
ghci> words "a b c"
["a","b","c"]
ghci> words "a   b                 c"
["a","b","c"]
ghci> words "          "
[]
ghci> words ""
[]
ghci> words "a                  "
["a"]
ghci> words "          a"
["a"]
ghci> words "          a\nbc\nd"
["a","bc","d"]
```
