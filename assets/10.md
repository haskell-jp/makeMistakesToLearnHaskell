# 関数を定義してリファクタリングする

前の課題、課題9のプログラムを書き換え、重複した箇所を関数として切り出しましょう。  
切り出した後も、元の課題9のプログラムと同じように動作するようにしてください。

## 必要な知識

### どこを切り出すか考える

```haskell
main = do
  putStrLn "Height Weight: "
  hwStr <- getLine
  case words hwStr of
    heightStr : weightStr : _ -> do
      let height = read heightStr
          weight = read weightStr
      print (weight / (height * height))
    [heightStr] -> do
      putStrLn "Weight: "
      weightStr <- getLine
      let height = read heightStr
          weight = read weightStr
      print (weight / (height * height))
    other ->
      putStrLn ("Invalid input: " ++ hwStr)
```

```haskell
      let height = read heightStr
          weight = read weightStr
      print (weight / (height * height))
```

課題4より

> Haskellでのプログラミングに限らず、プログラムの設計について考えるときは、可能な限り「入力や出力に関わる部分」と「（入力を受け取ってできた）**値に対して**（最終的に出力する前に）**どのような処理をするのか**」を分けて考えると、より再利用性が高く、テストも理解もしやすいプログラムが書けます。

というわけで、上記で抜粋した箇所から、出力に関係のある処理、つまり`print`関数を削除した、下記の部分を関数として切り出しましょう。

```haskell
      let height = read heightStr
          weight = read weightStr
      (weight / (height * height))
```

### 引数を受け取る関数を定義する

ここでは、引数を受け取る関数を定義する方法を学びましょう。  
これまでそうしてきたように、`main`関数に書いた処理に行いたい処理をすべて書き並べるだけでも、プログラムは書けます。  
しかし課題9の回答が自然とそうであったように、ある程度長いプログラムを書いていると、重複した箇所が発生することがしばしばあります。  
そのような箇所によい名前を与えて「共通した処理」として切り出すことは、プログラムを理解したり修正したりするときの重要な手がかりになります（ちなみに「よい名前」が与えられない場合は、例え重複していても無闇に切り出さない方がよいでしょう）。  
これはもちろんHaskellに限った話ではありません。

例として、BMIを計算して返す関数を紹介しましょう... と言いたいところですが、そのままそれをここで定義してしまうと課題の答えになってしまうので、代わりに「身長`heightStr`と体重`weightStr`を文字列として受け取って、BMIを求める**計算式**」を返す関数を`bmiExpression`という名前で書くことにします。

```haskell
bmiExpression heightStr weightStr = weightStr ++ " / (" ++ heightStr ++ " * " ++ heightStr ++ ")"
```

`bmiExpression`が関数の名前で、`heightStr`が一つ目の引数の名前、`weightStr`が二つ目の引数の名前です。`=`以降に、`bmiExpression`関数の定義、すなわち`bmiExpression`関数が引数を受け取って何をするのか、を書きます。

関数の定義が長い場合、途中で改行したくなりますよね。そういう場合は改行した次の行でインデント（行頭にスペースを一つ以上挿入）してください。

改行する位置は、変数名の途中などでなければどこでも構いません。こちら👇のようにイコール`=`の直後でも構いませんし、

```haskell
bmiExpression heightStr weightStr =
  weightStr ++ " / (" ++ heightStr ++ " * " ++ heightStr ++ ")"
```

イコール`=`の直前でも構いません:

```haskell
bmiExpression heightStr weightStr
  = weightStr ++ " / (" ++ heightStr ++ " * " ++ heightStr ++ ")"
```

（今回はそうでもないですが）引数のリストが長すぎると感じたら関数名の直後で改行してもいいですし、

```haskell
bmiExpression
  heightStr weightStr =
  weightStr ++ " / (" ++ heightStr ++ " * " ++ heightStr ++ ")"
```

関数の定義の本体、式の途中で改行しちゃっても構いません:

```haskell
bmiExpression heightStr weightStr = weightStr
  ++ " / ("
  ++ heightStr ++ " * " ++ heightStr
  ++ ")"
```

当然複数の箇所で改行してもよいので、これまでの例を組み合わせて次のように書くのもよいでしょう:

```haskell
bmiExpression heightStr weightStr =
  weightStr
  ++ " / ("
  ++ heightStr ++ " * " ++ heightStr
  ++ ")"
```

GHCは、関数定義の途中で改行していることに気づいた場合、インデントしている行が続く限り、つまり行頭にスペースを入力した行が続く限り、該当の関数の定義が続いていると解釈します。なので、👇のように最後の行がインデントされていない場合、エラーになってしまいます:

```haskell
bmiExpression heightStr weightStr =
  weightStr
  ++ " / ("
  ++ heightStr ++ " * " ++ heightStr
++ ")"
```

GHCiに`:{`と`:}`を使って入力しても、やっぱり受け入れてくれません:

```haskell
ghci> :{
ghci| bmiExpression heightStr weightStr =
ghci|   weightStr
ghci|   ++ " / ("
ghci|   ++ heightStr ++ " * " ++ heightStr
ghci| ++ ")"
ghci| :}

<interactive>:54:1: error: parse error on input ‘++’
```

### GHCiの`:l`と`:r`で、ファイルに定義した関数や型を読み込む

これを試す前に、まだ紹介していないGHCiの機能を紹介させてください。  
それは、`:l`（`:load`の略）と`:r`（`:reload`の略）です。  
`:l`コマンドは指定したHaskellのソースコードが書かれたファイルを読み、中に書かれている関数などを、GHCi上で直接扱えるようにしてくれます。  
さらに、`:l`コマンドで読んだファイルを修正した後、`:r`コマンドを使えば、それだけで再読込が出来ます！  
書いている関数や型の挙動をちょっとGHCiで確かめたい時にとても便利な機能なので、先ほど紹介した`bmiExpression`を試すのにも使ってみましょう。

（もっと早く紹介してもよかった...😰）

前節で定義した`bmiExpression`と言う関数を、例えば`bmiExpression.hs`という名前のファイルに保存した場合、`:l bmiExpression.hs`で、その定義を読み出すことが出来ます。

```haskell
ghci> :l bmiExpression.hs
[1 of 1] Compiling Main             ( bmiExpression.hs, interpreted )
Ok, one module loaded.
```

実際に`:l`コマンドで読み込んだ関数などの一覧を見るには、`:browse`コマンドを使用します。

```haskell
ghci> :browse
bmiExpression :: [Char] -> [Char] -> [Char]
```

定義した関数の名前を直接指定して、型宣言を見ること出来ます。

```haskell
ghci> :t bmiExpression
bmiExpression :: [Char] -> [Char] -> [Char]
```

型推論によって`bmiExpression`の引数と戻り値の型が自動で決定されているのがわかるでしょうか？  
この場合、`bmiExpression`は「文字列を2つ受け取って、文字列を返す関数」と解釈されています。

```haskell
ghci> bmiExpression "Height" "Weight"
"Weight / (Height * Height)"
ghci> bmiExpression "1.9" "45"
"45 / (1.9 * 1.9)"
```

### 純粋な関数の中でも（命令の中以外でも）`let`を使う

続いて、「**数値として**身長`height`と体重`weight`を受け取って、BMIを求める計算式」を返す関数を書いてみましょう。    
`Double`型の値を受け取る関数にするため、名前を`bmiExpressionFromDoubles`としましょう。

これはダメ

```haskell
bmiExpressionFromDoubles height weight =
  let heightStr = show height
      weightStr = show weight
  weightStr ++ " / (" ++ heightStr  ++ " * " ++ heightStr ++ ")"
```

```haskell
ghci> :l bmiExpressionFromDoubles.hs
[1 of 1] Compiling Main             ( bmiExpressionFromDoubles.hs, interpreted )

bmiExpressionFromDoubles.hs:3:3: error: parse error on input ‘let’
  |
3 |   let weightStr = show weight
  |   ^^^
Failed, no modules loaded.
```

```haskell
bmiExpressionFromDoubles height weight = do
  let heightStr = show height
      weightStr = show weight
  weightStr ++ " / (" ++ heightStr  ++ " * " ++ heightStr ++ ")"
```

ちなみに、

```haskell
bmiExpressionFromDoubles height weight = do
  let heightStr = show height
  let weightStr = show weight
  weightStr ++ " / (" ++ heightStr  ++ " * " ++ heightStr ++ ")"
```

### `let`の代わりに`where`を使って、トップダウンな定義を書く

※今回の課題を解くだけでは不要ですが、よくHaskellerが使うやり方なので、覚えておきましょう。

```haskell
bmiExpressionFromDoubles height weight =
  weightStr ++ " / (" ++ heightStr  ++ " * " ++ heightStr ++ ")"
 where
  heightStr = show height
  weightStr = show weight
```

関数のどこかで定義する変数を、関数の本体よりも後ろで定義できる。  
順番が逆なので慣れないうちは読みにくいかもしれませんが、関数の本体は、しばしば関数の全体像を把握するのに適しているので、トップダウンな読み書きができるよう、こちらを好んで使う人が多いように思います。

※個人的な所感として`let`/`in`はあまり使わないように思うので、ここでは触れない。

### 関数に型注釈をつける

```
ghci> :t bmiExpressionFromDoubles
bmiExpressionFromDoubles
  :: (Show a1, Show a2) => a2 -> a1 -> [Char]
```

`Show`型クラスのインスタンスである型`a1`と、同じく`Show`型クラスのインスタンスの型`a2`（`a1`の`a2`の型はそれぞれ違っていてもよい）の値をそれぞれ一つずつ受け取って、`[Char]`型（つまりは`String`）の値を返す関数となっています。

なるほど。`bmiExpressionFromDoubles`関数は`height`と`weight`、2つの引数に対して、`show`関数を実行して文字列に変換することしかしていないので、「`Show`型クラスのインスタンスであればなんでもよい」と解釈されたみたいですね！

しかし、今回はあくまでも関数の名前が`bmiExpressionFromDoubles`と名乗っているとおり、`Double`型の数値のみを受け取るようにしたいものです。  
というわけで、

```haskell
bmiExpressionFromDoubles :: Double -> Double -> String
bmiExpressionFromDoubles height weight =
  ...
```

`read "1" :: Integer`と書いて`read`関数にどの型を返すべきか教えていたのと同様に、`:: 型`という構文を使います。  
決定的に異なるのは、`関数名 :: 型`という書き方を関数本体の定義とは別に書くこと。
