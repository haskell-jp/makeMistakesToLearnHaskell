# 取り得る値を型で明示する・エラーを型宣言で明示する

以下のコマンドを持つ、簡単なインメモリーのデータベース（？）のプロトタイプを作りましょう。

```
add <keyとなる文字列> <整数>
get <keyとなる文字列>
quit
```

1. 「keyとなる文字列ごとに、整数を値として持つ連想配列`(A)`」を初期化します。
1. 以下の処理を繰り返し実行します。
    1. 標準入力から、文字列を1行分取得します。
    2. 受け取った行を空白で分割し、分割した結果に応じて、下記のような処理を実行します。
        - 分割した結果のリストの内容が「`add`という文字列で始まる、3つの要素を持つリスト」であった場合、3つめの要素の内容に応じて、以下のいずれかの処理を行います。
            - 2つめの要素を「keyとなる文字列」として、3つめの要素を「値となる整数」として解釈し、連想配列`(A)`における、keyに対応する値に追加し、`Adding <3つめの要素となる整数> to <> .`という形式の文字列を標準出力に出力します。
            - 3つめの要素が「値となる整数」として解釈できない文字列であった場合、`Error: Invalid Command`という文字列を標準出力に出力します。
        - 分割した結果のリストの内容が「`get`という文字列で始まる、2つの要素を持つリスト」であった場合、以下のいずれかの処理を行います。
            - 2つめの要素を「keyとなる文字列」として解釈し、連想配列`(A)`における、keyに対応する値が存在する場合、連想配列`(A)`から、keyに対応する整数を取り出し、<keyとなる文字列> => <対応する整数>`という形式の文字列を標準出力に出力します。
            - 2つめの要素を「keyとなる文字列」として解釈し、連想配列`(A)`における、keyに対応する値が存在しない場合、`Error: no item found`という文字列を標準出力に出力します。
        - 分割した結果のリストの内容が「`quit`という文字列のみを持つリスト」であった場合、繰り返しをやめ、プログラム全体を終了します。
        - それ以外の場合、`Error: Invalid Command`という文字列を標準出力に出力します。

## 必要な知識

今回の課題で習う新しい知識に加えて、これまでに習ったいろいろなことを再利用します。  
主要なものを

- 再帰処理
- `Map`型の値に対する`insertWith`

### 値の種類によって、保持できる値を切り替えられる型を作る

列挙型

```
data FoundOrNot = Found | NotFound
```

`|`で区切って「値コンストラクター」を列挙する

```
data FoundOrNot = Found | NotFound deriving Show
```

```
found = Found
:t found

```

`deriving Show`しているので`print`できる

```
print found
```

```
case found of
  Found -> "Found!"
  NotFound -> "Not Found!"
```

`Found`で見つかった値を持てない！

`Bool`型と何も変わらない。実際`Bool`型も同じ方法で定義されている。

```
ghci> :i Bool
data Bool = False | True        -- Defined in ‘GHC.Types’
-- ... 以下略 ...
```

`Found`で見つかった値を持つには？

```
data FoundOrNot = FoundInt Integer | NotFound deriving Show
--                         ^^^^^^^
--                      コンストラクターのパラメーター
```

```
ghci> data FoundOrNot = FoundInt Integer | NotFound deriving Show
ghci> :t FoundInt
FoundInt :: Integer -> FoundOrNot
ghci> :t NotFound
NotFound :: FoundOrNot
```

`FoundInt`が`Integer`を受け取るようになった！

課題6で、

```
data Entry =
  Entry
  { category :: String
  , price :: Integer
  }
```

という型を定義したとき、

```
ghci> :t Entry
Entry :: String -> Integer -> Entry
```

という型の、`Entry`という関数（値コンストラクター）ができていたのと同じ

`FoundInt`値コンストラクターに、`42`を渡せば答えが見つかる

```
ghci> FoundInt 42
FoundInt 42
```

でも、`NotFound`にはそんな引数はない

```
ghci> NotFound 42

<interactive>:13:1: error:
    ? Couldn't match expected type ‘Integer -> t’
                  with actual type ‘FoundOrNot’
    ? The function ‘NotFound’ is applied to one argument,
      but its type ‘FoundOrNot’ has none
      In the expression: NotFound 42
      In an equation for ‘it’: it = NotFound 42
    ? Relevant bindings include it :: t (bound at <interactive>:13:1)
```

```
ghci> NotFound
NotFound
```

パターンマッチで中の値を取り出すこともできる

```
ghci> lifeUniverseAndEverything = FoundInt 42
ghci> :{
ghci| case lifeUniverseAndEverything of
ghci|     FoundInt theAnswer -> print theAnswer
ghci|     NotFound -> putStrLn "Unknown"
ghci| :}
42
```

値コンストラクターに、存在しないパラメーターにマッチさせることは当然できない。

```
ghci> :{
ghci| case lifeUniverseAndEverything of
ghci|     FoundInt theAnswer -> print theAnswer
ghci|     NotFound theAnswerTwo -> print theAnswerTwo
ghci| :}

<interactive>:24:5: error:
    ? The constructor ‘NotFound’ should have no arguments, but has been given 1
    ? In the pattern: NotFound theAnswerTwo
      In a case alternative: NotFound theAnswerTwo -> print theAnswerTwo
      In the expression:
        case lifeUniverseAndEverything of
          FoundInt theAnswer -> print theAnswer
          NotFound theAnswerTwo -> print theAnswerTwo
```

これを利用することで、見つからなかった場合には`NotFound`を（引数なしで）返し、見つかった場合には`Found`を、見つかった値とともに返せば良くなる。

#### 型変数を追加して、`Integer`以外にも使えるようにする

`Found`は、`Integer`にしか使えない

```
ghci> Found "Life, the Universe and Everything"

<interactive>:6:1: error:
    ? Data constructor not in scope: Found :: [Char] -> t
    ? Perhaps you meant variable ‘round’ (imported from Prelude)
```

型引数を持った型を宣言する

```
ghci> data Found typeArgument = Found typeArgument | NotFound deriving Show
```

`typeArgument`が型引数

型引数によって具体的な型が割り当てられることによって、具体的な型になる

```
ghci> :t Found "a sock missing another"
Found "a sock missing another" :: Found [Char]
```

`Found [Char]` は`[Char]`（つまり文字列）に対する`Found`型。

`Found`値コンストラクターの値は文字列となる。

型引数に言及しない値コンストラクターでは、型引数のままになる。でも使える

```
ghci> :t NotFound
NotFound :: Found typeArgument
ghci> print NotFound
NotFound
```

アルファベット小文字で始まっていれば何でも型引数として扱われる。  
実際にはアルファベット小文字一文字で済ますことが多い。

```
ghci> data Found a = Found a | NotFound deriving Show
```

型引数によって具体的な型が割り当てられないと、関数の引数や戻り値として型注釈に書けない

```
ghci> :{
ghci| findSomething :: String -> Found
ghci| findSomething = error "the type annotations is illegal!"
ghci| :}

<interactive>:13:28: error:
    ? Expecting one more argument to ‘Found’
      Expected a type, but ‘Found’ has kind ‘* -> *’
    ? In the type signature: findSomething :: String -> Found
```

「kindエラー」というエラーが起こる。

kind: 「型の型」みたいなもの。当面は「型引数をいくつ受け取るか」という理解で十分。

型引数をいくつとる型かは`:k`（`:kind`の略）というGHCiのコマンドでわかる。

```
ghci| :k Found
Found :: * -> *
```

`*` が「普通の型」。関数の引数や戻り値になることができる型。
`* -> *`で、「『普通の型』を一つ受け取って、『普通の型』を返す型」になる。

### 実はすでにある: `Maybe`型

```
ghci> :i Maybe
data Maybe a = Nothing | Just a         -- Defined in ‘GHC.Maybe’
instance Applicative Maybe -- Defined in ‘GHC.Base’
-- ... 省略 ...
```

### `Map`型の値から、キーに対応する値を取り出す（その2）

`lookup`関数で、連想配列から例外を回避しつつ値を取り出す

```
ghci> import qualified Data.Map.Strict as M
ghci> :t M.lookup
M.lookup :: Ord k => k -> M.Map k a -> Maybe a
```

`Maybe a`を返している！

```
ghci> import qualified Data.Map.Strict as M
ghci> import           Data.Map.Strict ((!))
ghci> keyValues = M.fromList [("one", 1), ("two", 2)]
ghci> M.lookup "one" keyValues
Just 1
ghci> M.lookup "two" keyValues
Just 2
```

取り出すときは、`case`式で

```
ghci> :{
ghci| case M.lookup "one" keyValues of
ghci|     Just v -> show v
ghci|     Nothing -> "not found!"
ghci| :}
"1"

ghci> :{
ghci| case M.lookup "none" keyValues of
ghci|     Just v -> show v
ghci|     Nothing -> "not found!"
ghci| :}
"not found!"
```

当然、`Nothing`であった場合は値コンストラクターのパラメーターから取り出そうとしてもできない

```
ghci> :{
ghci| case M.lookup "one" keyValues of
ghci|     Just v -> show v
ghci|     Nothing v -> show v
ghci| :}

<interactive>:35:5: error:
    ? The constructor ‘Nothing’ should have no arguments, but has been given 1
    ? In the pattern: Nothing v
      In a case alternative: Nothing v -> show v
      In the expression:
        case M.lookup "one" keyValues of
          Just v -> show v
          Nothing v -> show v
```

### 発展編: 詳しいエラー情報を持つ、`Either`型

```
ghci> :i Either
data Either a b = Left a | Right b      -- Defined in ‘Data.Either’
instance Applicative (Either e) -- Defined in ‘Data.Either’
-- ... 省略 ...
```

慣習上、`Right`が成功した場合に返す値として使われ、`Left`が失敗した場合に返す値として使われる。

### 応用編: 実行しうるコマンドを表す型を作る

3つ以上の値コンストラクターを持つこともできる

今回の課題における、各種コマンドを表す

```
data Command =
    Add String Integer
  | Get String
  | Quit
  deriving Show
```

※今回の課題で必ず使わなくてもいいわけではない

コマンドを実行する処理と、入力をコマンドに変換する処理の分離がしやすくなる。

使わない場合とそうでない場合を試して比べてみるといいかも

使う場合のヒント: パターンマッチは入れ子になった構造に対して、中身に対してもマッチできるので、利用するとすっきり書けます。
